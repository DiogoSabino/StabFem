

mesh th= readmesh("mesh.msh");

real rho = 1;
real c = 1;
real Sin = int1d(th,2)(2*pi*x); // surface d'entrée
real Z0 = rho*c/Sin;// impedance carateristique du tuyau

complex Qin ;
real k; // nombre d'onde k=omega*c0
		    
fespace VH(th,P1);
VH<complex> f,f1,ftrace;
VH fr,fi,ft,fabs;
complex Zin, Pin;


problem Helmholtz(f,f1) =
         int2d(th)( (-(dx(f)*dx(f1)+dy(f)*dy(f1))+k^2*f*f1)*2*pi*x ) // - grad (phi) * grad(phi1) dV 

       - int1d(th,2)(f1*2*pi*x)  // condition d'entrée

	   + int1d(th,5)((1i*k-1/sqrt(x^2+y^2))*f*f1*2*pi*x) // condition de sortie de Sommerfeld (a utiliser pour les tuyaux) 
	   + int1d(th,6)(1i*k*f*f1*2*pi*x); // condition de sortie d'onde plane (a utiliser pour les pots d'échappements)

/// Début du programme principal 

	real kmin,kmax,dk;
	int choixparam;	
	cout << "##################@################" << endl  
	     << "## PROGRAMME Impedance_loop.edp  ##" << endl 
	     << "###################################" << endl << endl;
//	cout << "Parametres de la boucle : saisie au clavier (choix 1) ou lecture dans un fichier (choix 0) ?" << endl;
//	cin  >> choixparam;
	     
//	     if(choixparam==1)
	     {
	     	cout << "Entrez successivement kmin, dk, kmax : " << endl;
	     	cin >> kmin >> dk >> kmax; 
	     }
//	     else
//	     {
//	     	ifstream Input("Parameters_for_Impedance_loop.txt");  
//	        Input >> kmin >> dk >> kmax;
//	     };

	cout << endl << "BOUCLE avec kmin = " << kmin << " ; dk = " << dk << " ; kmax = " << kmax << endl;
	
	
	ofstream Res("Impedances.txt"); // declare un fichier de sortie qui s'appelle Res et correspond à Impedances.txt
	
	int Nb = (kmax-kmin)/dk+1;
	real[int] ktab(Nb);
	complex[int] Ztab(Nb);
	real[int] Reflexion(Nb);
	
	for(int index = 0; index<Nb; index++)
	{
	k= kmin+index*dk;	
	Helmholtz;
	
	Pin = -rho*c*1i*k*int1d(th,2)(2*pi*x*f)/Sin;
	Qin = int1d(th,2)(-2*pi*x*dy(f));
	Zin = Pin/Qin;
	
	ktab(index) = k;
	Ztab(index) = Zin;
	Reflexion(index) = abs((Zin-Z0)/(Zin+Z0))^2;
	
	cout << " k " << k << " ; Zin " << Zin  << " ; Ref = " << Reflexion(index) << endl;
	Res << k << " " << real(Zin) << " " << imag(Zin) << " " << abs(Zin) << " " << Reflexion(index) << " " << 1-Reflexion(index) <<  endl; 

	 
	} 

		{
		ofstream file("AcousticImpedances.ff2m");
		 file << "### Data generated by Freefem++ ; " << endl;
    	 file << "Impedance" << endl;
    	 file << "Format :" << endl;
    	 string descriptionFF;
    	 descriptionFF = " real."+Nb+" k " + " complex."+Nb+" Z "+" real."+Nb+" R "; 
	     file << descriptionFF << endl << endl ; 
		for(int j=0;j<Nb;j++) { file << ktab(j) << endl;};
		for(int j=0;j<Nb;j++) { file << real(Ztab(j)) << " " << imag(Ztab(j)) << endl;};
		for(int j=0;j<Nb;j++) { file << Reflexion(j)  << endl;};
		}

 
 
